plugins {
    id 'com.axelor.app'
}

group = 'com.educaflow.apps.expedientes'
version = '1.0.0'

axelor {
    title "educaFlow Expedientes"
    description "educaFlow Expedientes Módulo"
}

configurations {
    viewProcessorDependency
}
dependencies {
    implementation project(':modules:common')
    implementation 'org.modelmapper:modelmapper:3.1.1'

    viewProcessorDependency 'com.educaflow:AxelorXMLPreprocesor:1.0-SNAPSHOT'
}

tasks.register('viewProcessorTask', JavaExec) {
    description = 'Ejecuta una clase Java desde un JAR externo después de procesar los recursos.'
    group = 'build'

    mainClass = 'com.educaflow.common.buildtools.viewprocessor.Main'

    classpath = configurations.viewProcessorDependency


    classpath += sourceSets.main.runtimeClasspath

    args './src/main/resources/views', './build/resources/main/views'

    workingDir = layout.projectDirectory.dir('.')
}
tasks.named('processResources') {
    finalizedBy 'viewProcessorTask'
}