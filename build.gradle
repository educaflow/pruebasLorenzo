buildscript {
    repositories {
        mavenLocal()
    }
    dependencies {
        classpath 'com.axelor:axelor-gradle:7.4'
    }
}

plugins {
    id 'com.axelor.app'
    id("org.jetbrains.kotlin.jvm") version "2.2.0"
}



axelor {
    title = 'Pruebas Lorenzo'
}



allprojects {
    apply plugin: 'idea'
    apply plugin: 'eclipse'

    group = 'com.educaflow'
    version = '0.0.1'

    java {
        toolchain {
            languageVersion = JavaLanguageVersion.of(11)
        }
    }
    tasks.withType(JavaCompile) {
        options.compilerArgs << "-Xlint:deprecation"
    }
    afterEvaluate {
        configurations {
            // Remove Junit4, only keep Junit5 provided by AOP
            testImplementation.exclude group: 'junit', module: 'junit'
            testImplementation.exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
        }
        test {
            useJUnitPlatform()
            beforeTest { descriptor ->
                logger.lifecycle('Running: ' + descriptor)
            }
        }
    }
}


kotlin {
    compilerOptions {
        jvmTarget =  org.jetbrains.kotlin.gradle.dsl.JvmTarget.JVM_11
    }
}

configurations {
    educaFlowBuildToolsDependency
}

dependencies {
    implementation 'org.jetbrains.kotlin:kotlin-stdlib-jdk8' // O kotlin-stdlib-jdk7 o kotlin-stdlib
    implementation "org.jetbrains.kotlin:kotlin-reflect:2.2.0"
    testImplementation 'org.jetbrains.kotlin:kotlin-test'
    testImplementation 'org.jetbrains.kotlin:kotlin-test-junit' // O la que uses para testing (ej. kotlin-test-testng)

    educaFlowBuildToolsDependency 'com.educaflow:EducaFlowBuildTools:1.0-SNAPSHOT'
}

def isProduccion = project.hasProperty('environment') && project.environment == 'produccion'


tasks.named("compileKotlin").configure {
    dependsOn generateCode
}

tasks.register('viewProcessorTask', JavaExec) {
    description = 'Ejecuta una clase Java desde un JAR externo después de procesar los fichero de las vistas (form) de los expedientes.'
    group = 'build'

    mainClass = 'com.educaflow.common.buildtools.viewprocessor.Main'

    classpath = configurations.educaFlowBuildToolsDependency


    args './src/main/java', './build/resources/main/views'

    workingDir = layout.projectDirectory.dir('.')
}
tasks.named('processResources') {
    finalizedBy 'viewProcessorTask'
}



tasks.register('copyDataInit', JavaExec) {
    description = 'Ejecuta una clase Java desde un JAR externo para copiar las carpetas "data-init" de cada expediente.'
    group = 'build'

    mainClass = 'com.educaflow.common.buildtools.copyinitdata.Main'

    classpath = configurations.educaFlowBuildToolsDependency


    args './src/main/java', './build/resources/main/java-data-init'

    workingDir = layout.projectDirectory.dir('.')
}
tasks.named('build') {
    finalizedBy 'copyDataInit'
}
tasks.register('copyDomain', JavaExec) {
    description = 'Ejecuta una clase Java desde un JAR externo para copiar las los ficheros de dominio'
    group = 'build'

    mainClass = 'com.educaflow.common.buildtools.copydomain.Main'

    classpath = configurations.educaFlowBuildToolsDependency


    args './src/main/java', './build/resources/main/java-domain'

    workingDir = layout.projectDirectory.dir('.')
}
tasks.named('build') {
    finalizedBy 'copyDomain'
}

tasks.register('generateDataInitTiposExpedientes', JavaExec) {
    description = 'Ejecuta una clase Java desde un JAR externo para generar los ficheros de "data-init" de los tipos de expediente.'
    group = 'build'

    mainClass = 'com.educaflow.common.buildtools.createdatainittipoexpediente.Main'

    classpath = configurations.educaFlowBuildToolsDependency


    args './src/main/java', './build/resources/main'

    workingDir = layout.projectDirectory.dir('.')
}
tasks.named('build') {
    finalizedBy 'generateDataInitTiposExpedientes'
}



tasks.register('RichDomainClassTask', JavaExec) {
    description = 'Ejecuta una clase Java desde un JAR externo para añadir código al modelo con los enumerados de estados, eventos y profiles.'
    group = 'build'

    mainClass = 'com.educaflow.common.buildtools.richdomainclass.Main'

    classpath = configurations.educaFlowBuildToolsDependency


    args './src/main/java', './build/src-gen/java'

    workingDir = layout.projectDirectory.dir('.')
}
tasks.named('generateCode') {
    finalizedBy tasks.RichDomainClassTask
}

tasks.register('CreateFilesTask', JavaExec) {
    description = 'Ejecuta una clase Java desde un JAR externo para generar o validar los ficheros de un tipo de expediente.'
    group = 'build'

    mainClass = 'com.educaflow.common.buildtools.createfiles.Main'

    classpath = configurations.educaFlowBuildToolsDependency


    args './src/main/java'

    workingDir = layout.projectDirectory.dir('.')
}
tasks.named('generateCode') {
    dependsOn tasks.CreateFilesTask
}



war {
    archiveFileName = 'pruebaslorenzo.war'
}
jar {
    enabled = false
}
tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).configureEach {
    destinationDirectory.set(file("build/classes/java/main"))
}
apply from: 'gradle/style.gradle'